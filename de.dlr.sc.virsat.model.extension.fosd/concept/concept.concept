Concept de.dlr.sc.virsat.model.extension.fosd displayname "Feature-oriented development" description "Use FOSD concepts for easier spacecraft development." 
{
	Import de.dlr.sc.virsat.model.extension.ps.*;
	
	// --- Feature Tree ---
	StructuralElement FeatureTree description "Feature tree to describe common and variant spacecraft components." { 
		Inherits From [ProductTree];
		IsRootStructuralElement;
	}
	
	// --- Feature ---
	StructuralElement Feature description "Represents a spacecraft componenent." {    
		Inherits From [ElementDefinition, ProductTreeDomain];
		Applicable For [FeatureTree, Feature];
	}
	
	// --- SubFeatureRelationship ---
	Category SubFeatureRelationship description "Category to describe the relationship of the subfeatures." {
		Applicable For [Feature, FeatureTree];
		Cardinality 1;
		EnumProperty character values [or = "OR", xor = "XOR"];
	}
	
	Category OptionalRelationship description "Use this to mark the feature as optional." {
		Applicable For [Feature];
		Cardinality 1;
		BooleanProperty isOptional default true;
	}
	
	Category CrossTreeConstraint description "Reference to another feature and character of relationship." {
		Applicable For [Feature];
		StringProperty referenceUuid;
		EnumProperty character values [enumValue1 = "REQUIRES", enumValue2 = "EXCLUDES"];
	}
	
	Category MassBudget description "Max cumulated mass" {
		Applicable For [FeatureTree];
		Cardinality 1;
		FloatProperty mass;
	}
	
}

